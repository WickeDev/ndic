apply plugin: 'application'
apply plugin: 'kotlin-platform-jvm'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven-publish'
apply plugin: 'maven'

[compileKotlin, compileTestKotlin]*.configure {
    kotlinOptions.jvmTarget = JavaVersion.VERSION_1_8
}

def DefartifactId = rootProject.extensions.getByName("ext").artifactId
mainClassName = "${group}.${DefartifactId}.MainKt"

dependencies {
    expectedBy project(':ndic-common')
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    testCompile "junit:junit:4.12"
    testCompile "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"
    testCompile "org.jetbrains.kotlin:kotlin-test:$kotlin_version"

    testCompile('org.jetbrains.spek:spek-api:1.1.5') {
        exclude group: "org.jetbrains.kotlin"
    }
    testCompile('org.jetbrains.spek:spek-junit-platform-engine:1.1.5') {
        exclude group: "org.jetbrains.kotlin"
    }
    testCompile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    testCompile "com.winterbe:expekt:0.5.0"
    testCompile 'com.google.code.gson:gson:2.8.2'

    // Lib
    compile 'org.jsoup:jsoup:1.9.1'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}


install {
    repositories.mavenInstaller.pom.project {
        //noinspection GroovyAssignabilityCheck
        name project.name
        description 'Naver English-Korean dictionary Kotlin(Java compatible) Library'
        url 'https://github.com/wickedev/ndic'

        licenses {
            license {
                //noinspection GroovyAssignabilityCheck
                name 'The MIT License'
                url 'https://github.com/WickeDev/ndic/blob/master/LICENSE'
            }
        }
        developers {
            developer {
                id 'wickdev'
                //noinspection GroovyAssignabilityCheck
                name 'Ryan Yang'
                email 'orange881217@gmail.com'
            }
        }
        scm {
            connection "scm:git:git@github.com:wickedev/ndic.git"
            developerConnection "scm:git:git@github.com:wickedev/ndic.git"
            url "scm:git:git@github.com:wickedev/ndic.git"
        }
        issueManagement {
            system "github"
            url "https://github.com/wickedev/ndic/issues"
        }
    }
}

bintray {
    user = rootProject.properties['bintray.user']
    key = rootProject.properties['bintray.apiKey']
    configurations = ['archives']  //When uploading configuration files
    publish = true
    pkg {
        repo = rootProject.properties['bintray.repoName']
        name = project.name
        licenses = ['MIT']
        labels = ["naver", "dictionary", "ndic"]
        vcsUrl = 'https://github.com/wickedev/ndic.git'
        websiteUrl = 'https://github.com/wickedev'
        issueTrackerUrl = 'https://github.com/wickedev/ndic/issues'
        publicDownloadNumbers = true
        version {
            name = project.version
            desc = project.version
            vcsTag = project.version
            gpg {
                sign = true
                passphrase = rootProject.properties['bintray.gpgPassword']
            }
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            groupId group
            version project.version
            artifactId project.name
        }
    }
}