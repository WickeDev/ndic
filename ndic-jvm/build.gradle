apply plugin: 'application'
apply plugin: 'kotlin-platform-jvm'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven-publish'

[compileKotlin, compileTestKotlin]*.configure {
    kotlinOptions.jvmTarget = JavaVersion.VERSION_1_8
}

def DefartifactId = rootProject.extensions.getByName("ext").artifactId
mainClassName = "${group}.${DefartifactId}.MainKt"

dependencies {
    expectedBy project(':ndic-common')
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    testCompile "junit:junit:4.12"
    testCompile "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"
    testCompile "org.jetbrains.kotlin:kotlin-test:$kotlin_version"

    testCompile('org.jetbrains.spek:spek-api:1.1.5') {
        exclude group: "org.jetbrains.kotlin"
    }
    testCompile('org.jetbrains.spek:spek-junit-platform-engine:1.1.5') {
        exclude group: "org.jetbrains.kotlin"
    }
    testCompile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    testCompile "com.winterbe:expekt:0.5.0"
    testCompile 'com.google.code.gson:gson:2.8.2'

    // Lib
    compile 'org.jsoup:jsoup:1.9.1'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

bintray {
    user = rootProject.properties['bintray.user']
    key = rootProject.properties['bintray.apiKey']

    publications = ['mavenJava']   //When uploading Maven-based publication files
    configurations = ['archives']  //When uploading configuration files
    pkg {
        repo = rootProject.properties['bintray.repoName']
        name = project.name
        licenses = ['MIT']
        vcsUrl = 'https://github.com/WickeDev/ndic'
        publicDownloadNumbers = true
        version {
            name = project.version
            desc = project.version
            vcsTag = project.version
            gpg {
                sign = true
                passphrase = rootProject.properties['bintray.gpgPassword']
            }
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            groupId group
            version project.version
            artifactId project.name
        }
    }
    println configurations.runtime.allDependencies
}