plugins {
    id 'com.craigburke.karma' version '1.4.4'
}
apply plugin: 'kotlin-platform-js'

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-js:$kotlin_version"
    testCompile "org.jetbrains.kotlin:kotlin-test-js:$kotlin_version"
    expectedBy project(':')
}

compileKotlin2Js.configure {
    kotlinOptions.sourceMap = true
    kotlinOptions.moduleKind = "plain"
}

def libDir = "$buildDir/lib"
def compileOutput = compileKotlin2Js.outputFile
def testOutput = compileTestKotlin2Js.outputFile

task populateNodeModules(type: Copy, dependsOn: compileKotlin2Js) {
    configurations.testCompile.each {
        from zipTree(it.absolutePath).matching { include '*.js' }
    }

    into libDir
}

karma {
    dependencies(['mocha'])

    frameworks = ['mocha']
    browsers = ['Chrome']

    files = [
            "$libDir/kotlin.js",
            "$libDir/kotlin-test.js",
            "$compileOutput",
            "$testOutput"
    ]
}

karmaRun {
    dependsOn compileTestKotlin2Js
    dependsOn populateNodeModules
}

test.dependsOn karmaRun
clean.dependsOn karmaClean
